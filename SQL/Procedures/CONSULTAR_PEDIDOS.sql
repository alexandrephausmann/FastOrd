USE FASTORDER
GO

IF EXISTS (SELECT 1 
			FROM SYS.OBJECTS
			WHERE OBJECT_ID = OBJECT_ID('CONSULTAR_PEDIDOS')
					AND TYPE IN ('P','PC'))
	DROP PROCEDURE CONSULTAR_PEDIDOS

GO
CREATE PROC CONSULTAR_PEDIDOS
(
	@COD_PEDIDO INT = NULL,
	@COD_STATUS INT = NULL
)

AS

BEGIN
	DECLARE @MYEXECSQL NVARCHAR(MAX);
	DECLARE @PARAMLIST NVARCHAR(MAX);

	SET @MYEXECSQL = N'SELECT PEDIDO.COD_PEDIDO,
		   ITPEDIDO.COD_ITEM_PEDIDO,
		   ITPEDIDO.COD_PRODUTO,
		   PRODUTO.DESC_PRODUTO,
		   ITPEDIDO.QUANTIDADE,
		   PEDIDO.RETIRADA,
		   STSPEDIDO.DESC_STATUS_PEDIDO,
		   INTEGRAPRODUTO.DESC_TIPO_INTEGRACAO,
		   PEDIDO.RUA,
		   PEDIDO.BAIRRO,
		   PEDIDO.CEP,
		   PEDIDO.NUM_RESIDENCIA,
		   PEDIDO.DADO_COMPLEMENTAR,
		   PEDIDO.NUM_CELULAR
		   FROM TB_PEDIDO PEDIDO
		INNER JOIN TB_ITEM_PEDIDO ITPEDIDO
			ON PEDIDO.COD_PEDIDO = ITPEDIDO.COD_PEDIDO
		INNER JOIN TB_STATUS_PEDIDO STSPEDIDO
			ON PEDIDO.COD_STATUS_PEDIDO = STSPEDIDO.COD_STATUS_PEDIDO 
		INNER JOIN TB_PRODUTO PRODUTO
			ON ITPEDIDO.COD_PRODUTO = PRODUTO.COD_PRODUTO
		INNER JOIN TB_TIPO_INTEGRACAO INTEGRAPRODUTO
			ON PEDIDO.COD_TIPO_INTEGRACAO = INTEGRAPRODUTO.COD_TIPO_INTEGRACAO
		WHERE 1=1
		'
		IF @COD_PEDIDO IS NOT NULL
		BEGIN
			SET @MYEXECSQL = @MYEXECSQL + N' AND PEDIDO.COD_PEDIDO = @COD_PEDIDO'
		END

		IF @COD_STATUS IS NOT NULL
		BEGIN
			SET @MYEXECSQL = @MYEXECSQL + N' AND STSPEDIDO.COD_STATUS_PEDIDO = @COD_STATUS'
		END
	
	SELECT @PARAMLIST = '@COD_PEDIDO INT ,
						 @COD_STATUS INT'

	EXECUTE SP_EXECUTESQL  @MYEXECSQL,@PARAMLIST,@COD_PEDIDO,@COD_STATUS

END
GO

GRANT EXECUTE ON CONSULTAR_PEDIDOS TO PUBLIC
GO
